Story {

Story = Start? Node+

Start = "start: " ident

Node
  = BagTitle Predicate? (track|Passage|Choice|specialInstruction|Comment)* -- bag
  | GraphTitle (Passage|Choice|specialInstruction|Comment)* -- graph

BagTitle
  = "##" ident "##" -- end
  | "##" ident -- noEnd

GraphTitle
  = "#" ident "#" -- end
  | "#" ident -- noEnd

Predicate
  = "[" PredicateExp "]"

PredicateExp
  =
  | PredicateExp logicOperator PredicateExp -- chain
  | ifOperator PredicateExp -- explicit
  | "(" PredicateExp ")" -- parens
  | BooleanExp -- implicit


BooleanExp
   = ident existenceOperator -- exists
   | ident comparator ident -- comparison
   | ident -- truthy

// TODO: Rename passage
Passage
  = Predicate content -- predicate
  | content -- noPredicate

Choice
  = "->" ident ": " Predicate -- predicate
  | "->" ident -- noPredicate
  | "<->" Predicate Passage* -- inlineBagNode

// TODO: These aren't handled properly
// (should be embeddable ANYWHERE.
// Right now, they have to be their own line, and can only be used as a "Passage"
Comment
  = "--" sentence

content
  = ident ": " sentence

// Primitives
// TODO: steal judiciously from the ES5 example

ident  (an identifier)
  = symbol*

sentence
  = "\"" doubleQuoteSymbol* "\"" -- quote
  | unquotedTextSymbol* -- noQuote

doubleQuoteSymbol
  = unquotedTextSymbol | "\n"

unquotedTextSymbol
  = symbol | " "

symbol
  = alnum | "_" | "." | "!" | "," | "'" | "{" | "}" | "?" | "-"


comparator
  = "=="
  | "!="
  | "="
  | "<="
  | ">="
  | ">"
  | "<"
  | "isnt"
  | "is"

existenceOperator
  = "exists"
  | "doesnt exist"

ifOperator
  = "if"
  | "unless"
  | "not"
  | "if not"

logicOperator
  = "&&"
  | "||"
  | "and"
  | "or"

specialInstruction
  = "allowRepeats" | "deadEnd"

track
  = "track: " ident

}