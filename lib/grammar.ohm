Story {

Story = Comment? Start? (Comment|BagNode|GraphNode)+

Start = "start: " ident

BagNode = Title Predicate (track|Passage|Choice|specialInstruction|Comment)*
GraphNode = Title (Passage|Choice|specialInstruction|Comment)*

Title
  = header ident -- noEnd
  | header ident header -- end

Predicate
  = "[" PredicateExp "]"

PredicateExp
  =
  | PredicateExp logicOperator PredicateExp -- chain
  | ifOperator PredicateExp -- explicit
  | "(" PredicateExp ")" -- parens
  | BooleanExp -- implicit


BooleanExp
   = ident existenceOperator -- exists
   | ident comparator ident -- comparison
   | ident -- truthy

// TODO: Rename passage
Passage
  = Predicate content -- predicate
  | content -- noPredicate
  | VariableAssignment -- variable
  | Predicate VariableAssignment -- variableWithPredicate

VariableAssignment
  = "set" ident ("="|"to") ident

Choice
  = "->" ident ": " Predicate -- predicate
  | "->" ident -- noPredicate
  | "<->" Predicate (track|specialInstruction|Passage|Comment)* -- inlineBagNode

// TODO: These aren't handled properly
// (should be embeddable ANYWHERE.
// Right now, they have to be their own line, and can only be used as a "Passage"
Comment
  = "--" sentence

content
  = ident ": " sentence

// Primitives
// TODO: steal judiciously from the ES5 example

ident  (an identifier)
 = "\"" symbol* "\"" -- quoted
 | symbol* -- unquoted

sentence
  = "\"" doubleQuoteSymbol* "\"" -- quoted
  | unquotedTextSymbol* -- unquoted

doubleQuoteSymbol
  = unquotedTextSymbol | "\n"

unquotedTextSymbol
  = symbol | " "

symbol
  = alnum | "_" | "." | "!" | "," | "'" | "{" | "}" | "?" | "-"

header
  = "#" | "##"

comparator
  = "=="
  | "!="
  | "="
  | "<="
  | ">="
  | ">"
  | "<"
  | "isnt"
  | "is"

existenceOperator
  = "exists"
  | "doesnt exist"

ifOperator
  = "if"
  | "unless"
  | "not"
  | "if not"

logicOperator
  = "&&"
  | "||"
  | "and"
  | "or"

specialInstruction
  = "allowRepeats" | "deadEnd"

track
  = "track: " ident

}